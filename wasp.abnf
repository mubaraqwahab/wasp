; This grammar is written in the notation of the Augmented Backus-Naur Form (ABNF)
; as defined in RFC 5234: https://tools.ietf.org/html/rfc5234
;
; See https://www.eso.org/~ndelmott/ascii.html
; for a list of ASCII character codes.
;
; Inspiration for this file: https://github.com/toml-lang/toml/blob/master/toml.abnf
;
; Try the grammar interactively using instaparse: http://instaparse.mojombo.com/

; Overall program structure

program = [ ws-comment-nl ] s-expressions [ ws-comment-nl ]

; An s-expr may be separated from other exprs by a ws or comment
s-expressions = ( atom / list ) [ [ ws-comment-nl ] s-expressions ]

atom = number / symbol / string

list = [ single-quote ] open-bracket [ program ] close-bracket

single-quote = %x27
open-bracket = "("
close-bracket = ")"

; Whitespace

ws = 1*wschar
wschar = space / htab / newline
space = %x20
htab = %x09
newline = %x0A / %x0D.0A ; lf / crlf

ws-comment-nl = ws / 1*( [ comment ] newline )

; Number e.g. 1, -.0, 3., '45.6, -78e9, 1.2e-34

number = [ single-quote ] [ sign ] unsigned-number

unsigned-number =  int ( exponent / decimal-point *digit [ exponent ] )
unsigned-number =/ decimal-point int [ exponent ]

int = 1*digit
decimal-point = %x2E
exponent = "e" [ sign ] int
sign = "+" / "-"

; Symbols (and identifiers)
; * case insensitive
; * parentheses, quotes, periods, semicolons aren't allowed
; * can't be all numbers, can't start with number

symbol = [ single-quote ] symbol-char *( symbol-char / digit )

symbol-char = letter / symbol-punct

digit = %x30-39 ; 0-9
letter = %x41-5A / %x61-7A ; A-Z / a-z

symbol-punct =
  "`" / "~" / "!" / "@" / "#" / "$" / "%" / "^" / "&" / "*"
  / "-" / "_" / "=" / "+" / "[" / "{" / "]" / "}" / "\" / "|"
  / ":" / "," / "<" / ">" / "/" / "?"

; String: anything enclosed in double quotes
; Largely from the TOML grammar

string = [ single-quote ] double-quote *string-char double-quote

double-quote = %x22
string-char = unescaped / escaped
unescaped = wschar / %x21 / %x23-5B / %x5D-7E / non-ascii ; basically, everything except " and \
escaped = escape escape-seq-char
non-ascii = %x80-D7FF / %xE000-10FFFF

escape = %x5C           ; \
escape-seq-char =  %x22 ; "    quotation mark  U+0022
escape-seq-char =/ %x5C ; \    reverse solidus U+005C
escape-seq-char =/ %x62 ; b    backspace       U+0008
escape-seq-char =/ %x66 ; f    form feed       U+000C
escape-seq-char =/ %x6E ; n    line feed       U+000A
escape-seq-char =/ %x72 ; r    carriage return U+000D
escape-seq-char =/ %x74 ; t    tab             U+0009

; Comment starts with semicolon

comment = semicolon *non-eol

semicolon = %x3B
non-eol = %x09 / %x20-7F / non-ascii